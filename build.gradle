plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'dormitoryfamily'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// mysql connector
	runtimeOnly 'com.mysql:mysql-connector-j'

	// queryDSL 설정
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// aws S3
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

	//쿼리 파라미터 로그
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'

	//redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	//web socket
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

	// spring security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// OAuth2
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// dotenv-java
	implementation 'io.github.cdimascio:java-dotenv:+'

	//jackson-datatype-jsr310
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Querydsl 설정부
// 아래 것들이 없어도 기본적인 querydsl 동작은 하나 인테리제이에서 빌드 시 발생할 문제를 예방
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
// 원래 build 디렉토리 안에 있어서 눈에 안보였지만 꺼네서 내가 지정한 디렉토리에 꺼내옴
// 인텔리제이 IDE와의 문제인데, 빌드 gradle 할때 스캔 영역이 달라서 중복 스캔이 발생할 수 있다.
tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
	main.java.srcDirs += [ generated ]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
	delete file(generated)
}